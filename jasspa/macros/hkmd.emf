; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:    Sun Nov 15 2020
; Authors:    Detlef Groth
; Synopsis:   Markdown file hook (https://www.markdownguide.org/)
;
define-macro fhook-md
  set-variable #l0 &isin ".rmd" $buffer-bname "rmd" "md"
  ;ml-write #l0
  !if #l0
      @# buffer-init "md" "rmd"
  !else
      @# buffer-init "md" "md"
  !endif
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-md.name "Markdown"
set-variable .fhook-md.setup &reg "/history/fhook/md" "bfghijnopsx"
set-variable .fhook-md.setup-mask "abefghijkmnopstux"
set-variable .fhook-md.comment "|<!---|-->|-|  -- | --||"

; setup item-list
set-variable .fhook-md.item-list-s1 "^# \\(\.+\\)"
set-variable .fhook-md.item-list-r1 "H1 \ecB\\1\ecA"
set-variable .fhook-md.item-list-s2 "^## <a \.+>\\(\.+\\)</a>"
set-variable .fhook-md.item-list-r2 "H2 \ecB\\1\ecA"
set-variable .fhook-md.item-list-s3 "^## \\(\[^<\]\.+\\)"
set-variable .fhook-md.item-list-r3 "H2 \ecB\\1\ecA"

!if &sin "f" .fhook-md.setup
  ; setup emf collapsing
  set-variable .fhook-md.collapse-open  "^\\(# \\|## \\|---\\)"
  set-variable .fhook-md.collapse-close "^\\(# \\|## \\|---\\|\\'\\)"
  set-variable .fhook-md.collapse-mnext "-1"
!endif

!if &and &sin "h" .fhook-md.setup &band .hilight.flags 0x02
  !if &not &exi .hilight.md  
    set-variable .hilight.md &pinc .hilight.next 1
  !endif
  0 hilight .hilight.md 1               $global-scheme
  hilight .hilight.md 4 "<!--" "-->" "" .scheme.comment
  hilight .hilight.md 2 "^# "            .scheme.header
  hilight .hilight.md 2 "^## "           .scheme.header  
  hilight .hilight.md 2 "^### "           .scheme.bold
  hilight .hilight.md 2 "^```"          .scheme.string
  hilight .hilight.md 4 "\"" "\"" "\\"  .scheme.string
  ;hilight .hilight.md 4 "```" "```" "\\"   .scheme.string
  hilight .hilight.md 0 "'[^']+'"       .scheme.quote
  hilight .hilight.md 1 "__[^_]+__"     .scheme.bold
  hilight .hilight.md 1 "_[^_]+_"       .scheme.italic
  hilight .hilight.md 1 "`[^`]+`"       .scheme.quote
  ;xhilight .hilight.md 1 "\\[[^\\[]+\\)"       .scheme.quote
  ;hilight .hilight.md 0x804 "\\[" "\\]" "\\" .scheme.link
  ;hilight .hilight.md 0x804 "(" ")" "\\" .scheme.link  
  ;hilight .hilight.md 0x804 "(" ")" "\\" .scheme.link  
  ;hilight .hilight.md 4 "\\](" ")" "\\"   .scheme.link  
  ;hilight .hilight.md 0x44 "\\[" "\\](" "" ""   .scheme.link  
  hilight .hilight.md 0x300 "```{"
  hilight .hilight.md 0x300 "```{\\."
  
  ; Allow inserts into the markdown mode. You may add to the inserts in your mymd.emf file
  0 define-macro md-add-file-support
    set-variable #l0 @1
    !if &seq #l0 "py"
        set-variable #l0 "python"
        set-variable #l1 "py"
    !else
        set-variable #l1 #l0
    !endif
    ; Force the hilighting mode to load if not already loaded.
    !if &not &exi &cat ".hilight." #l0
      !force execute-file &cat "hk" #l0
      ml-write &cat "executed " #l0
    !endif
    ; If the hilighting mode is loaded then modify it.
    !if &sin "h" &ind &spr ".fhook-%s.setup" #l0 &band .hilight.flags 0x02 
      hilight .hilight.md 0x080 &spr "^```{%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x080 &spr "^```{\\.%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x080 &spr "^```{%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.md 0x080 &spr "^```{\\.%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight &ind &cat ".hilight." #l0 0x080 "^```" .hilight.md .scheme.hide
    !else
      hilight .hilight.md 0x104 &spr "```{%s}" #l1 "```" "" .scheme.quote
      hilight .hilight.md 0x104 &spr "```{\\.%s}" #l1 "```" "" .scheme.quote
      hilight .hilight.md 0x104 &spr "```{%s[, ][^}]+}" #l1 "```" "" .scheme.quote
      hilight .hilight.md 0x104 &spr "```{\\.%s[, ][^}]+}" #l1 "```" "" .scheme.quote
    !endif
  !emacro
  md-add-file-support "tcl"
  md-add-file-support "emf"
  md-add-file-support "r"
  md-add-file-support "dot"
  md-add-file-support "python"
  md-add-file-support "py"  
  md-add-file-support "sql"
!endif

!if &sin "x" .fhook-md.setup
  ; only define the b & e hooks if time stamping is enabled
  0 define-macro bhook-md
    set-variable .timestamp $timestamp
    set-variable $timestamp "%Y-%M-%D %h:%m"
  !emacro
  0 define-macro ehook-md
    set-variable $timestamp .bhook-md.timestamp
  !emacro
!endif

buffer-init-fhook "md" 

;define-help "md" 9
;
;! 9 md
;
;cEcESYNOPSIS cEcA
;
;
;    md, Rmd - Markdown and R-markdown plain text Markup
;
;
;cEcEFILES cEcA
;
;
;    cDhkmd.emfcA - Markdown text hook definition
;
;
;cEcEEXTENSIONS cEcA
;
;
;    cD.mdcA - Markdown markup files
;    cD.RmdcA - Markdown markup files with embedd R code
;
;
;cEcEDESCRIPTION cEcA
;
;
;    The cDmdcA file type template handles the hilighting and text formating of a
;    Markdown formatted text file.
;    
;    cDShort CutscA
;        The short cut keys used within the buffer are:-
;
;        cDC-c C-scA - Start Comment
;        cDC-c C-ccA - Comment Out Line
;        cDC-c C-dcA - Delete Comment
;        cDC-c C-ecA - Comment to End of Line
;        cDC-c C-ocA - Restyle Current Comment
;        
;        cDf2cA      - Open/Close Current Collapse
;        cDC-f2cA    - Open/Close All Collapses        
;        
;        cDS-f7cA    - Enable/Disable Auto-Spell
;        cDC-f7cA    - Auto-Spell Context Menu
;        
;        cDf6cA      - List Items (Headers)
;        cDC-f6cA    - Close List Items
;        
;cEcENOTES cEcA
;
;    This mode allows as well embedding of Python, R, Tcl code with syntax highlighting.
;    
;            ## Python code chunk example
;            
;            ```{py}
;            import sys
;            print(os.sys.getenv('HOME')
;            ```
;            
;            ## R code chunk example
;            
;            ```{r}
;            library(lattice)
;            with(iris,bwplot(Sepal.Height~Species))
;            ```
;     
;cEcESEE ALSO cEcA
;
;
;    lslmbuffer-setup(3)le, lslmspell-buffer(3)le.
;
;    lsMicroEmacs - Supported File TypeslmSupported File Typesle
;    
;!ehelp    
;
