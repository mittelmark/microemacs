;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;  Created By    : Detlef Groth, University of Potsdam, Germany
;  Created       : Mon Mar 24 11:14:06 2025
;  Last Modified : <251229.1119>
;
;  Description   : Support for editing language files with generic
;                  macros file-exec, file-format and file-lint using the 
;                  buffer provided variables with commands for
;                  formatting, linting and executing a source code file
;  Notes         : Based on tcltools.emf 
;     
;  Example       ; to activate the three commands you have to declare three buffer
;                ; variables for formatting, linting and executing the current buffer
;                ; for instance for Python files you could add the following three lines
;                ; to your mypython.emf or to your username.emf file
;                set-variable %fhook-python-format "ruff format %f" ; using the ruff formatter
;                set-variable %fhook-python-lint   "ruff check %f"    ; using the ruff linter
;                set-variable %fhook-python-exec   "python3 %f"
;                ; for the Go programming language it could be within mygo.emf
;                set-variable %fhook-go-format     "go fmt %f"
;                set-variable %fhook-go-lint       "go vet %f"
;                set-variable %fhook-go-exec       "go run %f"
;                ; for the C programming language
;                set-variable %fhook-c-exec "gcc %f -o %b && ./%b"
;                set-variable %fhook-c-lint "cppcheck %f"
;                set-variable %fhook-c-format "astyle --indent=spaces=4 %f"
;  History       : 2025-04-14 - first version
;                  2025-05-29 - rewrite using file-tools-run anf add-next-line macros
;                  2025-06-01 - using find.emf for the macros to locate the problematic line
;                  2025-06-21 - moving the global variables directly into this file to
;                               simplify installation
;
;  Copyright (c) 2025 Detlef Groth. License same as Jasspa's MicroEmacs
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; list of formatters: https://github.com/vim-autoformat/vim-autoformat
; list of linters: https://github.com/vim-syntastic/syntastic/tree/master/syntax_checkers
define-macro-file find return-get-next-line mouse-get-next-line

define-macro fhook-file-lint
  buffer-init-hooks
!emacro
; minimal defaults
add-next-line "*file-exec*" "%f:%l"
add-next-line "*file-lint*" "%f:%l"

define-macro file-lint
  set-variable #l0 &ind &cat &cat "%" $buffer-fhook "-lint"
  !if &seq #l0 "ERROR"
      -8 ml-write &cat &cat "Error: There is no variable %" $buffer-fhook "-lint declared!"
      !abort
  !endif
  &band @# 1 save-some-buffers @mna
  set-variable #l2 &spr "%s-replace"  $buffer-fhook
  delete-other-windows
  0x4188 file-tool-run #l0 $buffer-fname "*file-lint*"
  next-window-find-buffer "*file-lint*"
  !if &exi &ind #l2
    !force execute-named-command #l2
  !endif
  buffer-init "file-lint"
  set-variable :mouse-word-select "mouse-get-next-line"
  buffer-bind-key return-get-next-line "return"
!emacro  

set-variable .fhook-file-lint.name "File Lint"
set-variable .fhook-file-lint.setup &reg "/history/fhook/file-lint" "acfhmw"
set-variable .fhook-file-lint.setup-mask "acfhmw"

!if &and &sin "h" .fhook-file-lint.setup &band .hilight.flags 0x02
  !iif &not &exi .hilight.file-lint  set-variable .hilight.file-lint &pinc  .hilight.next 1
  0 hilight .hilight.file-lint 2 200                                     $global-scheme
  hilight .hilight.file-lint   0  "\\.+:[0-9]+:[0-9]*"      .scheme.link  
  hilight .hilight.file-lint   0  "[-_/A-Za-z0-9]+\\.*[A-Za-z0-9]+:[0-9]+:[0-9]*"      .scheme.link    
  hilight .hilight.file-lint   0  "warning:.+"      .scheme.operator
  hilight .hilight.file-lint   0  "error:.+"        .scheme.error
  hilight .hilight.file-lint   0  "WARNING.+"       .scheme.operator
  hilight .hilight.file-lint   0  "ERROR.+"         .scheme.error
!endif

add-file-hook "*file-lint*" fhook-file-lint

define-macro file-format
  ; on Windows the syntax for format-opts might be something like
  ; C:/Program Files/GO/../go.exe fmt %s
  set-variable #l0 &ind &cat &cat "%" $buffer-fhook "-format"
  set-variable #l1 $buffer-bname
  
  !if &seq #l0 "ERROR"
    -8 ml-write &cat &cat "Error: There is no variable %" $buffer-fhook "-format declared!"
    !abort
  !endif
  &band @# 1 save-some-buffers @mna  
  !if &seq #l0 "restyle-buffer"
    restyle-buffer
  !else
    0x4188 file-tool-run #l0 $buffer-fname "*file-format*"
    ;next-window-find-buffer #l1
    0 reread-file
  !endif
!emacro

define-macro fhook-file-exec
  buffer-init-hooks
!emacro

define-macro file-exec
  set-variable #l0 &ind &cat &cat "%" $buffer-fhook "-exec"
  set-variable #l1 $buffer-fhook
  !if &seq #l0 "ERROR"
      -8 ml-write &cat &cat "Error: There is no variable %" $buffer-fhook "-exec declared!"
      !abort
  !endif
  &band @# 1 save-some-buffers @mna
  ; command line arguments
  !iif &not &exi :file-exec-args  set-variable :file-exec-args ""
  !iif &not &band @# 2  set-variable :file-exec-args @ml2 "Additional command-line arguments" :file-exec-args 
  set-variable #l0 &spr "%s %s" #l0 :file-exec-args
  set-variable #l2 &spr "%s-replace"  $buffer-fhook
  ;0x80 ipipe-shell-command #l0 $buffer-fname "*file-exec*"
  delete-other-windows
  0x4188 file-tool-run #l0 $buffer-fname "*file-exec*"
  next-window-find-buffer "*file-exec*"
  !if &exi &ind #l2
    !force !force execute-named-command #l2
  !endif
  buffer-init "file-exec"
  set-variable :mouse-word-select "mouse-get-next-line"
  buffer-bind-key return-get-next-line "return"
!emacro

set-variable .fhook-file-exec.name "File Exec"
set-variable .fhook-file-exec.setup &reg "/history/fhook/file-exec" "acfhmw"
set-variable .fhook-file-exec.setup-mask "acfhmw"

!if &and &sin "h" .fhook-file-exec.setup &band .hilight.flags 0x02
  !iif &not &exi .hilight.file-exec  set-variable .hilight.file-exec &pinc  .hilight.next 1
  0 hilight .hilight.file-exec 2 200                                     $global-scheme
  hilight .hilight.file-exec   0  "[-_/\\.A-Za-z0-9]+:[0-9]+:[0-9]+"      .scheme.link    
  hilight .hilight.file-exec   0  "warning:.+"     .scheme.operator
  hilight .hilight.file-exec   0  "error:.+"       .scheme.error
  hilight .hilight.file-exec   0  "WARNING:.+"     .scheme.operator
  hilight .hilight.file-exec   0  "ERROR:.+"       .scheme.error
!endif

add-file-hook "*file-exec*" fhook-file-exec
; some code which should be moved into hkHOOK.emf files

0 define-macro fhook-generic-replace
  set-variable #l1 @1
  set-variable #l2 @2
  -1 buffer-mode "view"
  beginning-of-buffer
  replace-string #l1 #l2
  beginning-of-buffer
  -1 buffer-mode "edit"
  1 buffer-mode "view"  
!emacro

; checs for the existance of file and 
; sets the $ return variable to 1 if it exists
; and to 0 if it dees not exists

define-macro file-exists
  set-variable #l1 @1
  set-variable $file-names #l1
  set-variable #l0 &spr "%s" $file-names
  !if &and &not &seq #l0 "" &sin #l0 #l1
    set-variable $result 1
  !else
    set-variable $result 0
  !endif
!emacro

; awk - hkawk.emf
; C3 - hkc3.emf
; Cobol - hkcobol.emf
; Dart - hkdart.emf
; Euphoria -hkeuphor.emf
; Fortran - hkf90.emf
; Go - hkgo.emf
; Java - hkjava.emf
; julia - hkjula.emf
; Lua - hklua.emf
; Octave - hkoctave.emf
; Pascal - hkpascal.emf
; Perl - hkperl.emf
; Python - hkpython.emf
; R - hkr.emf
; Rust - hkrust.emf
; Tcl - hktcl.emf
; V -  hkv.emf
; Zig - hkzig.emf



