; -!- emf -!-
; This is part of the JASSPA MicroEmacs macro files
; Copyright (C) 1999-2009 JASSPA (www.jasspa.com)
; See the file me.emf for copying and conditions.
;
; Created:    Sun Nov 15 2020
; Authors:    Detlef Groth
; Synopsis:   Markdown file hook (https://www.markdownguide.org/)
;
define-macro fhook-typst
  @# buffer-init "typst"
  buffer-init-hooks
!emacro

; buffer-init variables
set-variable .fhook-typst.name "Typst"
set-variable .fhook-typst.setup &reg "/history/fhook/typst" "bdfghijnopswx"
set-variable .fhook-typst.setup-mask "abdefghijkmnopstuwx"
set-variable .fhook-typst.comment "|/*| */|*| * | * ||"

; setup item-list
set-variable .fhook-typst.item-list-s1 "^= \\(\.+\\)"
set-variable .fhook-typst.item-list-r1 "H1 \ecB\\1\ecA"
set-variable .fhook-typst.item-list-s3 "^== \\(\[^<\]\.+\\)"
set-variable .fhook-typst.item-list-r3 "H2 \ecB\\1\ecA"

!if &sin "f" .fhook-typst.setup
  ; setup emf collapsing
  set-variable .fhook-typst.collapse-open  "^\\(= \\|== \\)"
  set-variable .fhook-typst.collapse-close "^\\(= \\|== \\|\\'\\)"
  set-variable .fhook-typst.collapse-mnext "-1"
!endif

!if &not &exist .hilight.typst
    set-variable .hilight.typst &pinc .hilight.next 1
!endif

!if &and &sin "h" .fhook-typst.setup &band .hilight.flags 0x02
  0 hilight .hilight.typst 1               $global-scheme
  hilight .hilight.typst 20 "/\\*" "*/" "" .scheme.comment
  hilight .hilight.typst 2 "//"           .scheme.comment

  hilight .hilight.typst 2 "^= "              .scheme.header
  hilight .hilight.typst 2 "^== "             .scheme.header  
  hilight .hilight.typst 2 "^=== "            .scheme.bold
  hilight .hilight.typst 2 "^```"             .scheme.string
  hilight .hilight.typst 4 "\"" "\"" ""       .scheme.string
  hilight .hilight.typst 4 "\\$" "\\$" "\\"   .scheme.string  
  hilight .hilight.typst 4 "\\*\\w+" "\\*" "" .scheme.bold
  hilight .hilight.typst 1 "_[^_]+_"          .scheme.italic
  hilight .hilight.typst 1 "`[^`]+`"          .scheme.quote
  hilight .hilight.typst 4  "#" "\\}\\W" ""   .scheme.keyword
  hilight .hilight.typst 4  "@" "\\}\\W" ""   .scheme.link
  hilight .hilight.typst 4  "<" ">\\}\\W" ">" .scheme.link  
  hilight .hilight.typst 0x300 "```{"
  hilight .hilight.typst 0x300 "```{\\."
  
  ; Allow inserts into the markdown mode. You may add to the inserts in your mymd.emf file
  0 define-macro typst-add-file-support
    set-variable #l0 @1
    !if &seq #l0 "py"
        set-variable #l0 "python"
        set-variable #l1 "py"
    !else
        set-variable #l1 #l0
    !endif
    ; Force the hilighting mode to load if not already loaded.
    !if &not &exi &cat ".hilight." #l0
      !force execute-file &cat "hk" #l0
      ml-write &cat "executed " #l0
    !endif
    ; If the hilighting mode is loaded then modify it.
    !if &sin "h" &ind &spr ".fhook-%s.setup" #l0 &band .hilight.flags 0x02 
      hilight .hilight.typst 0x080 &spr "^```%s" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.typst 0x080 &spr "^```{%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.typst 0x080 &spr "^```{\\.%s}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.typst 0x080 &spr "^```{%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight .hilight.typst 0x080 &spr "^```{\\.%s[, ][^}]+}" #l1 &ind &cat ".hilight." #l0 .scheme.hide
      hilight &ind &cat ".hilight." #l0 0x080 "^```" .hilight.typst .scheme.hide
    !else
      hilight .hilight.typst 0x104 &spr "```%s" #l1 "```" "" .scheme.quote      
      hilight .hilight.typst 0x104 &spr "```{%s}" #l1 "```" "" .scheme.quote
      hilight .hilight.typst 0x104 &spr "```{\\.%s}" #l1 "```" "" .scheme.quote
      hilight .hilight.typst 0x104 &spr "```{%s[, ][^}]+}" #l1 "```" "" .scheme.quote
      hilight .hilight.typst 0x104 &spr "```{\\.%s[, ][^}]+}" #l1 "```" "" .scheme.quote
    !endif
  !emacro
  typst-add-file-support "tcl"
  typst-add-file-support "emf"
  typst-add-file-support "r"
  typst-add-file-support "dot"
  typst-add-file-support "python"
  typst-add-file-support "py"  
  typst-add-file-support "sql"
!endif

!if &sin "x" .fhook-typst.setup
  ; only define the b & e hooks if time stamping is enabled
  0 define-macro bhook-typst
    set-variable .timestamp $timestamp
    set-variable $timestamp "%Y-%M-%D %h:%m"
  !emacro
  0 define-macro ehook-typst
    set-variable $timestamp .bhook-typst.timestamp
  !emacro
!endif

!if &sin "d" .fhook-typst.setup
    0 indent  .hilight.typst 0 10
    indent .hilight.typst n "{"  t
    indent .hilight.typst o "}" -t
    indent .hilight.typst n "("  t
    indent .hilight.typst o ")" -t
    indent .hilight.typst n "\\["  t
    indent .hilight.typst o "]" -t
    indent .hilight.typst e "\"" "\"" ""
    indent .hilight.typst i "^\\s*//"
!endif
buffer-init-fhook "typst" 

