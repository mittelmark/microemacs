; -!- emf -!-
;
; Copyright (C) 1998-2024 JASSPA (www.jasspa.com)
;
; This is part of JASSPA's MicroEmacs, see the LICENSE file for licensing and
; copying information.
;
; Synopsis:    Command-line macro for creating a php tags file
; Authors:     Steven Phillips
;
; Usage:
;      me "@phptags" [<files> <dirs>] [-f -o<options> -t<tag_file> -m<file_mask> -i<ignore_files>]
;
; Options:
;      r - recurse, a - append, m - multiple links to a tag
;
set-variable %tag-option   ""
set-variable %tag-file     "tags"
set-variable %tag-filemask ":*.php:*.php3:*.php4:*.php5:*.phtml:*.inc:"
set-variable %tag-ignore   ":SCCS/:CVS/:.git/:.svn/:"
; set $auto-time to 0 to stop issues with any existing <file># files
set-variable $auto-time 0

set-variable #l0 0
!while &exi &cat ".about.arg" #l0
  set-variable #l2 &ind &cat ".about.arg" &pinc #l0 1
  set-variable #l1 &lef #l2 2
  set-variable #l2 &rig #l2 2
  !if &seq #l1 "-o"
    set-variable %tag-option #l2
  !elif &seq #l1 "-t"
    set-variable %tag-file #l2
  !elif &seq #l1 "-m"
    set-variable %tag-filemask #l2
  !elif &seq #l1 "-i"
    set-variable %tag-ignore #l2
  !endif
!done

set-char-mask "3" "$."

define-macro phptags-add-file
  set-variable $buffer-mask "luh13"
  set-variable #l2 $buffer-fname
  !if &seq &lef #l2 %tag-plen %tag-path
    set-variable #l2 &rig #l2 %tag-plen
  !endif
  ml-write &spr "[Parsing file %s]" #l2
  1 buffer-mode "magic"
  beginning-of-buffer
  !repeat
    !force search-buffer "me" "\\<\\(function\\|class\\)\\s+\\(\\w+\\)"
    !if $status
      set-variable #l1 @s2
      set-variable #l3 @wl
      next-window
      !tjump &sin "m" %tag-option 4
      beginning-of-buffer
      !force search-forward &spr "^%s\t" #l1
      !if &not $status
        end-of-buffer
        insert-string #l1
        insert-string "\t"
        insert-string #l2
        insert-string "\t/^"
        insert-string #l3
        insert-string "$/"
      !endif
      next-window
    !endif
    !force forward-line
  !until &not $status
!emacro

define-macro phptags-parse-tags
  ml-write "All done! Parsing tags file."
  beginning-of-buffer
  set-mark
  replace-string "\\\\" "\\\\\\\\"
  end-of-buffer
  sort-lines
!emacro

define-macro phptags-process-buffers
  !repeat
    set-variable #l0 $buffer-bname
    !if &xse #l0 "^.*<[0-9]*>$"
      set-variable #l1 &lef #l0 &sub &rsin "<" #l0 1
    !else
      set-variable #l1 #l0
    !endif
    !if &iseq #l1 %tag-bname
      !return
    !elif &lfind %tag-ignore #l1
    !elif &band $buffer-fmod 0x10000
      set-variable #l2 $buffer-fname
      set-variable #l3 0
      !while &not &seq &set #l4 &lget %tag-filemask &inc #l3 1 ""
        !force find-file &cat #l2 #l4 
        !iif &seq $buffer-fname &cat #l2 #l4  !force 0 delete-buffer $buffer-bname
      !done
    !else
      beginning-of-buffer
      !force phptags-add-file
      !if &not $status
        ml-write &spr "[Warning: phptags-add-file failed on \"%s\"]" $buffer-fname
        !bell
        ;set-variable #l9 @cq
      !endif
    !endif
    0 delete-buffer #l0
  !until 0
!emacro

define-macro phptags
  !if &seq &lget %tag-filemask 1 ""
    ml-write "[No input file mask set!]"
    !abort
  !endif
  delete-other-windows
  split-window-vertically
  find-buffer "*scratch*"
  set-variable #l0 &stat "a" "."
  find-file %tag-file
  set-variable %tag-bname $buffer-bname 
  -1 buffer-mode "view"
  !force 0 delete-buffer "*scratch*"
  set-variable %tag-path &lef $buffer-fname &set %tag-plen &rsin "/" $buffer-fname
  !if &not &sin "a" %tag-option
    beginning-of-buffer
    set-mark
    end-of-buffer
    -1 kill-region
  !else
    end-of-buffer
  !endif
  !if &sin "r" %tag-option
    set-variable %tag-filemask &lins %tag-filemask -1 "[^.]*/"
  !endif
  1 buffer-mode "magic"
  1 buffer-mode "exact"
  next-window
  !if &iseq $buffer-bname %tag-bname
    !force find-file #l0
  !endif
  phptags-process-buffers
  phptags-parse-tags
!emacro

0 define-macro start-up
  phptags
  quick-exit
!emacro

; load in user extensions if found
!force execute-file "myphptags"
